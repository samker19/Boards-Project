#include <SPI.h>
#include <Wire.h>
#include <Tiny4kOLED.h>
#include "font11x16.h"
#include "font16x32digits.h"
#include <Ultrasonic.h>

#define BUZZER_PIN 1
#define TRIGGER_PIN 3
#define ECHO_PIN 4
#define OLED_WIDTH 128
#define OLED_HEIGHT 32

Ultrasonic ultrasonic(TRIGGER_PIN, ECHO_PIN);
unsigned long distance;

void setup() { 
  pinMode(BUZZER_PIN, OUTPUT);
  oled.begin(OLED_WIDTH, OLED_HEIGHT, sizeof(tiny4koled_init_128x32br), tiny4koled_init_128x32br);
  oled.setFont(FONT8X16);
  oled.on();
  oled.clear();
  oled.setCursor(54, 32);
  oled.print("Jeem3.com");
  delay(2000);
  oled.clear();
}

void displayDistance(int dist) {
  oled.clear();
  oled.setCursor(46, 32);
  oled.setFont(FONT16X32DIGITS);
  oled.print(dist);
}

void reverseBeep(int distance) {
  if (distance < 5) {
    // Continuous sound when the distance is less than 5
    digitalWrite(BUZZER_PIN, HIGH);
  } else if (distance <= 35) {
    int beepDuration;
    int beepInterval;

    if (distance <= 10) {
      beepDuration = 50;
      beepInterval = 100;
    } else if (distance <= 15) {
      beepDuration = 100;
      beepInterval = 200;
    } else if (distance <= 25) {
      beepDuration = 150;
      beepInterval = 300;
    } else if (distance <= 35) {
      beepDuration = 200;
      beepInterval = 400;
    }

    digitalWrite(BUZZER_PIN, HIGH);
    delay(beepDuration);
    digitalWrite(BUZZER_PIN, LOW);
    delay(beepInterval);
  } else {
    digitalWrite(BUZZER_PIN, LOW); 
    delay(1000);
  }
}

void loop() {
  distance = ultrasonic.read();

  if (distance > 0 && distance <= 100) {
    displayDistance(distance);
    reverseBeep(distance);
  } else {
    oled.clear();
    oled.setCursor(16, 32);
    oled.setFont(FONT8X16);
    oled.print("Out of Range");
    digitalWrite(BUZZER_PIN, LOW); // Ensure buzzer is off when out of range
    delay(1000); // Delay when out of range
  }
}
